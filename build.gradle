plugins {
    id 'application'
    id 'groovy'
    id 'java'
    id 'io.freefair.lombok' version '6.4.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'

        showExceptions true
        exceptionFormat 'full'
        showCauses true
        showStackTraces true

        showStandardStreams false
    }
}

group 'es.sralloza'
version '1.0-SNAPSHOT'
mainClassName = 'RunStrava'

repositories {
    mavenCentral()
}

dependencies {
    // Testing
    testImplementation 'org.mockito:mockito-core:4.4.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    // Selenium
    implementation 'org.seleniumhq.selenium:selenium-java:4.1.3', {
        transitive = false
    }
    implementation 'org.seleniumhq.selenium:selenium-api:4.1.3'
    implementation 'org.seleniumhq.selenium:selenium-support:4.1.3'
    implementation 'org.seleniumhq.selenium:selenium-firefox-driver:4.1.3'

    // Utils
    implementation 'com.typesafe:config:1.4.2'
    implementation 'org.projectlombok:lombok:1.18.22'
    implementation 'com.google.inject:guice:5.1.0'

    // Logging
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.tersesystems.logback:logback-classic:1.0.2'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.1.1'
    implementation 'com.tersesystems.logback:logback-structured-config:1.0.2'
}


task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

    manifest {
        attributes 'Main-Class': mainClassName
    }

    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
